testclass bike_computer_test for statechart Statechart {
	@Test
	operation ride_state_active_on_init () {
		enter
		
		assert called disable_alarm 1 times
		assert called display_unlocked 1 times
		assert active (main_region.Ride)
	}

	@Test
	operation secured_state_on_left_click () {
		ride_state_active_on_init
		
		raise left_button_clicked
		
		assert called display_locked 1 times
		assert active (main_region.Secured)
	}

	@Test
	operation alarm_state_on_motion_detected () {
		secured_state_on_left_click
		
		raise motion_detected
		
		assert called activate_alarm 1 times
		assert active (main_region.Alarm)
	}

	@Test
	operation password_prompt_state_on_left_click_given_locked () {
		secured_state_on_left_click
		
		raise left_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}
	
	@Test
	operation password_prompt_state_on_left_click_given_alarm () {
		alarm_state_on_motion_detected
		
		raise left_button_clicked

		assert active (main_region.PasswordPrompt.r1.Ready)
	}
	
	@Test
	operation input_short_char_on_left_click() {
		password_prompt_state_on_left_click_given_locked
		
		input_short_char
		assert called new_password_input(2) 1 times
	}
	
	@Test
	operation input_long_char_on_right_click() {
		password_prompt_state_on_left_click_given_locked
		
		input_long_char
		assert called new_password_input(3) 1 times
	}
	
	@Test
	operation ride_state_on_correct_password_given_locked () {
		password_prompt_state_on_left_click_given_locked
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	@Test
	operation ride_state_on_correct_password_given_alarm () {
		password_prompt_state_on_left_click_given_alarm
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	
	@Test
	operation ride_state_on_wrong_password_then_correct_password_given_locked () {
		password_prompt_state_on_left_click_given_locked
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		
		input_correct_password
		assert active (main_region.Ride)
	}
	
	@Test
	operation ride_state_on_wrong_password_then_correct_password_given_alarm () {
		password_prompt_state_on_left_click_given_alarm
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		
		input_correct_password
		assert active (main_region.Ride)
	}
	
	
	@Test
	operation multiple_locks_unlocks() {
		password_prompt_state_on_left_click_given_locked
		input_correct_password
		assert active (main_region.Ride)
		
		raise left_button_clicked
		
		assert called display_locked 1 times
		assert active (main_region.Secured)
		
		raise motion_detected
		
		assert called activate_alarm 1 times
		assert active (main_region.Alarm)
		
		raise left_button_clicked
		assert active (main_region.PasswordPrompt.r1.Ready)
		
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		
		input_correct_password
		assert active (main_region.Ride)
	}
	
	operation input_correct_password() {
		mock verify_password(114) returns (true)
		
		input_long_char
		assert called new_password_input(3) 1 times
		input_long_char
		assert called new_password_input(7) 1 times
		input_short_char
		assert called new_password_input(14) 1 times
		input_short_char
		assert called new_password_input(28) 1 times
		input_long_char
		assert called new_password_input(57) 1 times
		input_short_char
		assert called new_password_input(114) 1 times
		
		raise double_button_clicked
		
		assert called verify_password(114) 1 times
	}

	operation input_wrong_password() {
		mock verify_password(70) returns (false)
		
		input_short_char
		assert called new_password_input(2) 1 times
		input_short_char
		assert called new_password_input(4) 1 times
		input_short_char
		assert called new_password_input(8) 1 times
		input_long_char
		assert called new_password_input(17) 1 times
		input_long_char
		assert called new_password_input(35) 1 times
		input_short_char
		assert called new_password_input(70) 1 times
		
		raise double_button_clicked
		
		assert called verify_password(70) 1 times
	}
	
	operation input_short_char() {
		raise left_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}	
	
	operation input_long_char() {
		raise right_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}
}
