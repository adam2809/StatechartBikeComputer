testclass bike_computer_test for statechart Statechart {
	@Test
	operation ride_state_active_on_init () {
		enter
		
		assert active (main_region.Ride)
	}

	@Test
	operation secured_state_on_left_click () {
		ride_state_active_on_init
		
		raise left_button_clicked
		
		assert active (main_region.Secured)
	}

	@Test
	operation alarm_state_on_motion_detected () {
		secured_state_on_left_click
		
		raise motion_detected
		
		assert active (main_region.Alarm)
	}

	@Test
	operation password_prompt_state_on_left_click_given_locked () {
		secured_state_on_left_click
		
		raise left_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}
	
	@Test
	operation password_prompt_state_on_left_click_given_alarm () {
		alarm_state_on_motion_detected
		
		raise left_button_clicked

		assert active (main_region.PasswordPrompt.r1.Ready)
	}
	
	@Test
	operation input_short_char_on_left_click() {
		password_prompt_state_on_left_click_given_locked
		
		input_short_char
	}
	
	@Test
	operation input_long_char_on_right_click() {
		password_prompt_state_on_left_click_given_locked
		
		input_long_char
	}
	
	@Test
	operation alarm_state_on_wrong_password_given_locked_and_exceeded_tries () {
		password_prompt_state_on_left_click_given_locked 
		
		exceed_max_password_tries
	}
	
	@Test
	operation alarm_state_on_wrong_password_given_alarm_and_exceeded_tries () {
		password_prompt_state_on_left_click_given_alarm
		
		exceed_max_password_tries
	}
	
	@Test
	operation ride_state_on_correct_password_given_locked () {
		password_prompt_state_on_left_click_given_locked
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	@Test
	operation ride_state_on_correct_password_given_alarm () {
		password_prompt_state_on_left_click_given_alarm
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	
	@Test
	operation ride_state_on_wrong_password_then_correct_password_given_locked () {
		password_prompt_state_on_left_click_given_locked
		input_wrong_password
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	@Test
	operation ride_state_on_wrong_password_then_correct_password_given_alarm () {
		password_prompt_state_on_left_click_given_alarm
		input_wrong_password
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	operation input_correct_password() {
		input_long_char
		input_long_char
		input_short_char
		input_short_char
		input_long_char
		input_short_char
		
		raise double_button_clicked
		
		assert correct_password
	}
	
	operation exceed_max_password_tries() {
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		input_wrong_password
		assert active (main_region.Alarm)
	}

	operation input_wrong_password() {
		input_short_char
		input_short_char
		input_short_char
		input_long_char
		input_long_char
		
		raise double_button_clicked
		
		assert wrong_password
	}
	
	operation input_short_char() {
		raise left_button_clicked
		
		assert password_prompt_short_char
		assert active (main_region.PasswordPrompt.r1.Ready)
	}	
	
	operation input_long_char() {
		raise right_button_clicked
		
		assert password_prompt_long_char
		assert active (main_region.PasswordPrompt.r1.Ready)
	}
}
