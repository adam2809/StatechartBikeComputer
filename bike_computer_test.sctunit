testclass bike_computer_test for statechart Statechart {
	@Test
	operation ride_state_active_on_init () {
		enter
		
		assert called disable_alarm 1 times
		assert called display_unlocked 1 times
		assert active (main_region.Ride)
	}

	@Test
	operation secured_state_on_left_click () {
		ride_state_active_on_init
		
		raise left_button_clicked
		
		assert called display_locked 1 times
		assert active (main_region.Secured)
	}

	@Test
	operation alarm_state_on_motion_detected () {
		secured_state_on_left_click
		
		raise motion_detected
		
		assert called activate_alarm 1 times
		assert active (main_region.Alarm)
	}

	@Test
	operation password_prompt_state_on_left_click_given_locked () {
		secured_state_on_left_click
		
		raise left_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}
	
	@Test
	operation password_prompt_state_on_left_click_given_alarm () {
		alarm_state_on_motion_detected
		
		raise left_button_clicked

		assert active (main_region.PasswordPrompt.r1.Ready)
	}
	
	@Test
	operation input_short_char_on_left_click() {
		password_prompt_state_on_left_click_given_locked
		
		input_short_char
		assert called new_password_input(0) 1 times
	}
	
	@Test
	operation input_long_char_on_right_click() {
		password_prompt_state_on_left_click_given_locked
		
		input_long_char
		assert called new_password_input(1) 1 times
	}
	
	@Test
	operation alarm_state_on_wrong_password_given_locked_and_exceeded_tries () {
		password_prompt_state_on_left_click_given_locked 
		
		exceed_max_password_tries
	}
	
	@Test
	operation alarm_state_on_wrong_password_given_alarm_and_exceeded_tries () {
		password_prompt_state_on_left_click_given_alarm
		
		exceed_max_password_tries
	}
	
	@Test
	operation ride_state_on_correct_password_given_locked () {
		password_prompt_state_on_left_click_given_locked
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	@Test
	operation ride_state_on_correct_password_given_alarm () {
		password_prompt_state_on_left_click_given_alarm
		input_correct_password
		
		assert active (main_region.Ride)
	}
	
	
	@Test
	operation ride_state_on_wrong_password_then_correct_password_given_locked () {
		password_prompt_state_on_left_click_given_locked
		input_wrong_password
		assert called is_password_tries_exceeded(1) 1 times
		assert active (main_region.PasswordPrompt.r1.Ready)
		
		input_correct_password
		assert active (main_region.Ride)
	}
	
	@Test
	operation ride_state_on_wrong_password_then_correct_password_given_alarm () {
		password_prompt_state_on_left_click_given_alarm
		input_wrong_password
		assert called is_password_tries_exceeded(1) 1 times
		assert active (main_region.PasswordPrompt.r1.Ready)
		
		input_correct_password
		assert active (main_region.Ride)
	}
	
	
	@Test
	operation stress_test() {
		
	}
	
	operation input_correct_password() {
		mock verify_password(50) returns (true)
		
		input_long_char
		assert called new_password_input(1) 1 times
		input_long_char
		assert called new_password_input(3) 1 times
		input_short_char
		assert called new_password_input(6) 1 times
		input_short_char
		assert called new_password_input(12) 1 times
		input_long_char
		assert called new_password_input(25) 1 times
		input_short_char
		assert called new_password_input(50) 1 times
		
		raise double_button_clicked
		
		assert called verify_password(50) 1 times
	}
	
	operation exceed_max_password_tries() {
		mock_password_tries_exceeded
		
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		assert called is_password_tries_exceeded(1) 1 times
		
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		assert called is_password_tries_exceeded(2) 1 times
		
		input_wrong_password
		assert active (main_region.PasswordPrompt.r1.Ready)
		assert called is_password_tries_exceeded(3) 1 times
		
		input_wrong_password
		assert active (main_region.Alarm)
		assert called is_password_tries_exceeded(4) 1 times
	}
	
	operation mock_password_tries_exceeded() {
		mock is_password_tries_exceeded(1) returns (false)
		mock is_password_tries_exceeded(2) returns (false)
		mock is_password_tries_exceeded(3) returns (false)
		mock is_password_tries_exceeded(4) returns (true)
	}

	operation input_wrong_password() {
		mock verify_password(50) returns (false)
		
		input_short_char
		assert called new_password_input(0) 1 times
		input_short_char
		assert called new_password_input(0) 1 times
		input_short_char
		assert called new_password_input(0) 1 times
		input_long_char
		assert called new_password_input(1) 1 times
		input_long_char
		assert called new_password_input(3) 1 times
		input_short_char
		assert called new_password_input(6) 1 times
		
		raise double_button_clicked
		
		assert called verify_password(6) 1 times
	}
	
	operation input_short_char() {
		raise left_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}	
	
	operation input_long_char() {
		raise right_button_clicked
		
		assert active (main_region.PasswordPrompt.r1.Ready)
	}
}
